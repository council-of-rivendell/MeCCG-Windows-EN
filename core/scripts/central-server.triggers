#
#    Gccg - Generic collectible card game.
#    Copyright (C) 2001-2013 Tommi Ronkainen
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program, in the file license.txt. If not, write
# to the Free Software Foundation, Inc., 59 Temple Place - Suite 330,
# Boston, MA 02111-1307, USA.
#
##############################################################################
#
# Combined central server
#
##############################################################################

trigger "init" "" code
{
  game.data=game_data();
  GAME=game.data{"game"};
  GAMEDIR=game.data{"gamedir"};
  
  execute("common.include");
  execute("central-server.functions");
  if(!execute(GAMEDIR+"-central-server.include"))
  {
    println("");
    println(GAMEDIR+"-central-server.include not defined for this game");
    println("");
    quit(1);
  }
  
  randomize();
  tables=(,);
  draft.tables=(,);
  players=(,);
  set.data=(,);
  forall("set.data{#}=set_data(#)",sets());
  rarities=rarities();
  Log("Rarities found: "+join(rarities," "));
  
  Log("Loading old data... ");

  users=(,);

  if(binary_load("users"))
  {
    attach("users","DBStringKeys");
    delsaved("users");
    delsaved("users.bak");
  }
  else
    attach("users","DBStringKeys");

  if(!binary_load("prices"))
    prices=(,);
  if(!binary_load("ban"))
    ban=(,);
  if(!binary_load("tableban"))
    tableban=(,);
  if(!binary_load("restrict"))
    restrict=(,);

  cache_parameters("users",(5,900,5,25));
  
  Initialize();
}

trigger "init" "server" code
{
}

trigger "error" "network" code
{
  Log("NETWORK ERROR: "+net_client_name(event)+" message: "+error.message);
  stacktrace();
  errors=errors+1;
  set_error_trigger("","");
}

trigger "error" "command" code
{
  Log("ERROR: "+net_client_name(event)+" message: "+error.message);
  if(error.echo!=NULL)
    Send(error.echo,("Message","{red}ERROR: "+net_client_name(event)+" message: "+error.message));
  
  stacktrace();
  errors=errors+1;
  set_error_trigger("","");
}

trigger "init" "game" code
{
  Log("Initializing central server at port "+port);
  net_create_server(port);
}

trigger "main" "" code
{
  push(e);
  e=net_server_get();
  if(e!=NULL)
  {
    if(first(e)=="quit")
      quit(1);
    else
      ReceiveData(e);
  }
  e=pop();
}

trigger "exit" "" code
{
  SaveAll();
}

